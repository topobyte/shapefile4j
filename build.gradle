wrapper {
    gradleVersion = '5.2.1'
}

allprojects {
    group = pGroup
    version = pVersion
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'eclipse'

    repositories {
        maven {
            url maven_path_topobyte
        }
        maven {
            url 'http://mvn.topobyte.de'
        }
        mavenCentral()
    }
}

project(':shapefile-lib') {
    dependencies {
        compile 'org.slf4j:slf4j-api:1.6.1'
        runtime 'org.slf4j:slf4j-log4j12:1.6.1'
        runtime 'log4j:log4j:1.2.16'
        compile 'org.locationtech.jts:jts-core:1.16.1'
        compile 'commons-codec:commons-codec:1.10'
        compile 'topobyte:xBaseJ-topobyte:0.0.3'
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: maven_url_topobyte)
            }
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }
}

project(':shapefile-lib-util') {
    dependencies {
        compile project(':shapefile-lib')
        compile 'de.topobyte:simple-mapfile-xml:0.1.0'
        compile 'de.topobyte:commons-cli-helper:0.1.0'
    }
    task copyJars(type: Copy) {
        into "$buildDir/lib-run"
        from jar
    }

    task copyDeps(type: Copy) {
        into "$buildDir/lib-run"
        from configurations.runtime
    }

    task createRuntime(dependsOn: [copyJars, copyDeps]) {
        description 'Setup the execution environment for the main scripts'
    }
}
