wrapper {
    gradleVersion = '7.6.1'
}

buildscript {
    repositories {
        maven { url 'https://mvn.topobyte.de' }
    }
    dependencies {
        classpath 'de.topobyte:gradle-launch-and-install-plugin:0.0.5'
    }
}

allprojects {
    group = pGroup
    version = pVersion
}

project(':shapefile4j') {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
}

project(':shapefile4j-util') {
    apply plugin: 'application'
}

subprojects {
    apply plugin: 'eclipse'

    repositories {
        maven {
            url 'https://mvn.topobyte.de'
        }
        mavenCentral()
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

project(':shapefile4j') {
    dependencies {
        api 'de.topobyte:dbf4j:0.1.0'
        api 'org.locationtech.jts:jts-core:1.19.0'
        api 'commons-codec:commons-codec:1.16.0'
        api 'org.slf4j:slf4j-api:1.7.36'
    }

    if (project.hasProperty("topobyte")) {
        apply from: 'file:///' + System.getProperty("user.home") \
            + '/.gradle/topobyte.gradle'
    }

    java {
         withSourcesJar()
    }

    publishing {
        publications {
            library(MavenPublication) {
                from components.java
            }
        }
    }
}

project(':shapefile4j-util') {
    apply plugin: 'de.topobyte.launch-and-install-gradle-plugin'

    startScripts.enabled = false

    launchAndInstall {
        installationPath = 'topobyte/shapefile4j'
        makeselfLabel = 'Shapefile4j'
        createScript(project, 'de.topobyte.esri.shapefile.executables.DumpData', 'ShapefileDumpData')
        createScript(project, 'de.topobyte.esri.shapefile.executables.ShowFields', 'ShapefileShowFields')
        createScript(project, 'de.topobyte.esri.shapefile.executables.ToSmxCollection', 'ShapefileToSmxCollection')
    }

    dependencies {
        implementation project(':shapefile4j')
        implementation 'de.topobyte:simple-mapfile-xml:0.1.0'
        implementation 'de.topobyte:commons-cli-helper:0.1.0'

        runtimeOnly 'org.slf4j:slf4j-log4j12:1.6.1'
        runtimeOnly 'log4j:log4j:1.2.16'
    }
    task copyJars(type: Copy) {
        into "$buildDir/lib-run"
        from jar
    }

    task copyDeps(type: Copy) {
        into "$buildDir/lib-run"
        from configurations.runtimeClasspath
    }

    task createRuntime(dependsOn: [copyJars, copyDeps]) {
        description 'Setup the execution environment for the main scripts'
    }
}
